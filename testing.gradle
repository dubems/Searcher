apply plugin: 'docker-compose'
apply plugin: 'com.adarshr.test-logger'

test {
    useJUnitPlatform {
        excludeTags 'integration-test'
    }
    testLogging {
        testlogger {
            theme 'standard'
        }
    }
}

// ====== integration tests config
task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'integration-test'
    }
    testLogging {
        testlogger {
            theme 'standard'
            slowThreshold 1000
        }
    }
    check.dependsOn it
    shouldRunAfter test
}

integrationTest.doFirst {
    dockerCompose.exposeAsSystemProperties(integrationTest)
}

dockerCompose.isRequiredBy(integrationTest)

dockerCompose {
    useComposeFiles = ['docker-compose.yml']
    waitForTcpPorts = true
    captureContainersOutput = project.hasProperty('docker-compose-log')
    removeVolumes = true
    projectName = UUID.randomUUID()
    dockerComposeWorkingDirectory = "${projectDir}/src/test/resources/"
    dockerComposeStopTimeout = java.time.Duration.ofSeconds(5)
    environment.put 'HOSTNAME_EXTERNAL', project.findProperty('docker-network-host') ?: 'localhost'
}